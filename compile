#!/usr/bin/env coffee

fs = require 'fs'
files = fs.readdirSync 'messages/'

fieldParser = 
    version: (value) ->
        ret = []
        m = value.match /([\d.]+)( - ([\d.]+))?/
        ret.push m[1]
        ret.push m[3] if m[3]
        return ret
    message: (value) ->
        return value.toString()
    description: (value) ->
        return value.toString()


parsePart = (part, obj) ->
    s = part.split ':'
    key = s.shift()
    value = s.join(':').replace(/\n/g, ' ').replace(/\s+/g, ' ').replace(/^\s*(.*?)\s*$/, '$1')
    obj[key] = fieldParser[key] value

fileToMessage = (file) ->
    contents = fs.readFileSync "messages/#{file}"
    parts = contents.toString().split '\n\n'

    ret = { id: file }
    parsePart part, ret for part in parts
    return ret

messages = (fileToMessage file for file in files)
fs.writeFileSync './public/germ.db', JSON.stringify messages
